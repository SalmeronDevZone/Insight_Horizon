EVALUATE

{
/*--------------------------------------------------------------------
Medida para calcular el N.º de clientes considerados nuevos.
--------------------------------------------------------------------*/
--Variables que almacenan las fechas que acotan los tres períodos-----
VAR _fechUltFra =   DATE(2020, 3, 3)
--VAR _fechUltFra =   MAX( factVentas[fecha] )
VAR _nMesesRecien = 2 //Personalizar N.º meses del período reciente
VAR _nMesesInterm = 2 //Personalizar N.º meses del período intermed
VAR _nMesesAntig =  12 - _nMesesRecien - _nMesesInterm
VAR _postRecient =  EOMONTH( _fechUltFra, -1 ) + 1
VAR _iniReciente =  EDATE( _postRecient, - _nMesesRecien )
VAR _finIntermed =  _iniReciente - 1
VAR _iniIntermed =  EDATE ( _iniReciente, -_nMesesInterm )
VAR _iniAntiguo =   EDATE( _iniIntermed, -_nMesesAntig )
VAR _antesAntiguo = _iniAntiguo - 1

--Variable que almacena los clientes en el período reciente-----------
VAR _cliRecientes = CALCULATETABLE(
                        VALUES( factPedidos[ID_cliente] ),
                        dimCalendario[fecha] > _finIntermed,
                        dimCalendario[fecha] < _postRecient
                    )

--Variable que almacena los clientes en el per. intermedio + antiguo--
VAR _cliInterAnt =  CALCULATETABLE(
                        VALUES( factPedidos[ID_cliente] ),
                        dimCalendario[fecha] < _iniReciente,
                        dimCalendario[fecha] > _antesAntiguo
                    )

--Variable que almacena una tabla con los clientes nuevos-------------
VAR _nuevos =       EXCEPT( _cliRecientes, _cliInterAnt )

--Variable que almacena el recuento de clientes nuevos----------------
VAR _result =       COUNTROWS( _nuevos )

RETURN              _result

}