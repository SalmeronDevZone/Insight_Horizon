dimFechas = 

--Variables para almacenar los años mínimo y máximo---------------
VAR _ejercicioMin =     YEAR( MIN( factPedidos[fechaPedido] ) )
VAR _ejercicioMax =     YEAR( MAX( factPedidos[fechaPedido] ) )

--Variables para almacenar las fechas mínima y máxima-------------
VAR _fechaMin =         DATE( _ejercicioMin, 1, 1 )
VAR _fechaMax =         DATE( _ejercicioMax, 12, 31 )

--Variable para determinar el primer mes fiscal-------------------
VAR _primerMesFisc =    7  --Adaptar según necesidades

RETURN

ADDCOLUMNS (
    --Generación de tabla para columna de fecha-------------------
    CALENDAR ( _fechaMin, _fechaMax ),

    --Generación de columnas--------------------------------------
    "Año",              YEAR( [Date] ),
    "Trimestre",        "Trim-" & QUARTER( [Date] ),
    "Nombre mes",       FORMAT( [Date], "mmm" ),
    "N.º mes",          MONTH( [Date] ),
    "Mes-año",          FORMAT( [Date], "mmm-yy" ),
    "N.º año-mes",      VALUE( FORMAT( [Date], "yyyymm" ) ),
    "Nombre día semana",FORMAT( [Date], "DDDD" ),
    "N.º día semana",   WEEKDAY( [Date], 2 ),
                        --Adaptar según necesidades

    --Otras columnas complementarias------------------------------
    "Es día laborable", IF(
                            WEEKDAY( [Date], 2 ) <= 5,
                            --Adaptar según necesidades
                            "Día laborable",
                            "Fin de semana"
                        ),
    "Semana del año",   WEEKNUM( [Date], 21 ),
                        --Adaptar según necesidades
    "Semestre",         IF(
                            MONTH( [Date] ) <= 6,
                            "Semestre 1",
                            "Semestre 2"
                        ),

    --OPCIONAL: clave subrogada para relacionar-------------------
    "IDS_fecha",        YEAR( [Date] ) * 10000 + 
                        MONTH( [Date] ) * 100 + 
                        DAY( [Date] ),

    --Columnas para gestión de años fiscales----------------------
    "Año fiscal",       IF(
                            MONTH( [Date] ) >= _primerMesFisc,
                            "AF " & YEAR( [Date] ) + 1,
                            "AF " & YEAR( [Date] )
                        ),
    "Semestre fiscal",  VAR _mesFiscal = MOD(
                                            MONTH( [Date] ) -
                                            _primerMesFisc + 12,
                                            12
                                        )
                                        + 1
                        RETURN
                        "Semestre " &
                        IF( _mesFiscal <= 6, 1, 2),
    "Trim. fiscal",     VAR _mesFiscal =    MOD(
                                                MONTH( [Date] ) -
                                                _primerMesFisc + 12,
                                                12
                                            )
                                            + 1
                        RETURN
                        "Trim-" &
                        SWITCH(
                            TRUE(),
                            _mesFiscal >= 1 && _mesFiscal <= 3, 1,
                            _mesFiscal >= 4 && _mesFiscal <= 6, 2,
                            _mesFiscal >= 7 && _mesFiscal <= 9, 3,
                            4
                        )
)
